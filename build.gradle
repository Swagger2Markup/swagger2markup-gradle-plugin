buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
        //classpath "com.gradle.publish:plugin-publish-plugin:0.9.0"
    }
}

group = 'io.github.swagger2markup'
version = '1.0.1-SNAPSHOT'
description = 'A Swagger to Markup (AsciiDoc and Markdown) converter Gradle Plugin.'

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.jfrog.artifactory"
apply from: 'gradle/publishing.gradle'
//apply plugin: 'java-gradle-plugin'
//apply plugin: "com.gradle.plugin-publish"

tasks.withType(GroovyCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile ("io.github.swagger2markup:swagger2markup:1.0.1")
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude group: "org.codehaus.groovy"
    }
    testCompile 'io.github.robwin:assertj-diff:0.1.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }
    publish {
        repository {
            repoKey = 'oss-snapshot-local' //The Artifactory repository key to publish to
            //when using oss.jfrog.org the credentials are from Bintray. For local build we expect them to be found in
            //~/.gradle/gradle.properties, otherwise to be set in the build server
            username = project.hasProperty('bintrayUsername') ? project.bintrayUsername : System.getenv('BINTRAY_USER')
            password = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : System.getenv('BINTRAY_KEY')
        }
        defaults {
            publications('mavenJava')
        }
    }
    if (System.properties['https.proxyHost']) {
        clientConfig.proxy.host = System.properties['https.proxyHost']
        clientConfig.proxy.port = System.properties['https.proxyPort'].toInteger()
    }

}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.coveralls {
    dependsOn 'check'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/*
pluginBundle {
    website = 'https://github.com/RobWin/swagger2markup-gradle-plugin'
    vcsUrl = 'https://github.com/RobWin/swagger2markup-gradle-plugin'
    description = project.description
    tags = ['swagger', 'asciidoc', 'markdown']

    plugins {
        swagger2MarkupPlugin {
            id = 'io.github.robwin.swagger2markup'
            displayName = 'Swagger2Markup Gradle plugin'
        }
    }
}
*/

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
